plugins {
    id 'dev.architectury.loom' version '1.7-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = "${rootProject.minecraft_version}+${rootProject.mod_version}+${rootProject.mod_version_label}"
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        archivesName = "$rootProject.mod_name"
    }

    repositories {
        mavenLocal()
        mavenCentral() {
            content {
                includeGroup 'org.projectlombok'
            }
        }
        maven {
            url "https://www.cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }
        maven { url 'https://maven.nova-committee.cn/s3' }
        maven { url 'https://maven.minecraftforge.net/' }
        maven { url 'https://maven.parchmentmc.org' }
        maven { url "https://maven.shedaniel.me/" }
        maven { url "https://maven.terraformersmc.com/releases/" }
        maven { url "https://api.modrinth.com/maven"}
    }

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.officialMojangMappings()
    }

    java {
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 17
    }

    tasks.withType(ProcessResources).configureEach {

        var replaceProperties = [
                'mod_version'                  : mod_version,
                "minecraft_version"            : minecraft_version,
                "forge_version"                : forge_version,
                "forge_loader_version_range"   : forge_loader_version_range,
                "forge_version_range"          : forge_version_range,
                "minecraft_version_range"      : minecraft_version_range,
                "fabric_api_version"           : fabric_api_version,
                "fabric_loader_version"        : fabric_loader_version,
                "mod_name"                     : mod_name,
                "mod_author"                   : mod_author,
                "mod_id"                       : mod_id,
                "license"                      : license,
                "description"                  : project.description,
                "neoforge_version"             : neoforge_version,
                "neoforge_loader_version_range": neoforge_loader_version_range,
                "credits"                      : credits
        ]

        inputs.properties replaceProperties

        filesMatching(['META-INF/*mods.toml', 'fabric.mod.json', 'pack.mcmeta']) {
            expand replaceProperties
        }
    }

    // Configure Maven publishing.
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
        repositories {
            // Add repositories to publish to here.
            // Notice: This block does NOT have the same function as the block in the top level.
            // The repositories here will be used for publishing your artifact, not for
            // retrieving dependencies.
        }
    }
}
